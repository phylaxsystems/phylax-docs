---
title: 'PCL Quickstart'
description: 'A quickstart guide to using the PCL CLI'
---

## Introduction

This guide will walk you through the process of creating, testing, and submitting an assertion using the PCL (Phylax Credible Layer) CLI. By the end of this tutorial, you'll understand how to:

1. Set up your project structure
2. Write a simple assertion
3. Test your assertion
4. Build and store your assertion
5. Submit your assertion to the Credible Layer

- **⚠️ TODO: add a link to the github repo where the code can also be found**
- **⚠️ TODO: update code examples to use a more interesting contract and assertion**

## Prerequisites

- PCL CLI installed (see the [Installation Guide](credible/credible-install))
- Basic knowledge of Solidity
- A wallet for authentication

## 1. Project Setup

First, let's set up a project with the correct directory structure:

```bash
mkdir pcl-project
cd pcl-project

# Create the required directories
mkdir -p assertions/src assertions/test src lib
```

Next, let's create the smart contract that our assertion will monitor. This is a simple `Ownable` contract that tracks ownership of a contract.

Create a file `src/Ownable.sol`:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.28;

contract Ownable {
    address private _owner;

    event OwnershipTransferred(address indexed previousOwner, address indexed newOwner);

    constructor() {
        _owner = msg.sender;
        emit OwnershipTransferred(address(0), _owner);
    }

    modifier onlyOwner() {
        require(_owner == msg.sender, "Ownable: caller is not the owner");
        _;
    }

    // Get the current owner
    function owner() public view returns (address) {
        return _owner;
    }

    function transferOwnership(address newOwner) public onlyOwner {
        require(newOwner != address(0), "Ownable: new owner is the zero address");
        emit OwnershipTransferred(_owner, newOwner);
        _owner = newOwner;
    }
}
```

## 2. Writing Your First Assertion

Assertions in PCL are Solidity contracts that inherit from the `Assertion` base contract. They define checks that can be run against smart contracts to verify specific properties.

Let's create a simple assertion that checks if an `Ownable` contract's ownership has changed after a transaction.

Create a file `assertions/src/OwnableAssertion.sol`:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.28;

import {Assertion} from "credible-std/Assertion.sol"; // Credible Layer precompiles
import {Ownable} from "../../src/Ownable.sol"; // Ownable contract

contract OwnableAssertion is Assertion {
    Ownable ownable;

    constructor(address ownable_) {
        ownable = Ownable(ownable_); // Define address of Ownable contract
    }

    // Define selectors for the assertions
    // This function is required by the Assertion interface
    function triggers() external view override {
        registerCallTrigger(this.assertionOwnershipChange.selector); // Register the selector
    }

    // This function checks if the ownership has changed
    function assertionOwnershipChange() external {
        ph.forkPreState(); // Fork the pre-state of the transaction
        address preOwner = ownable.owner(); // Get the owner before the transaction
        
        ph.forkPostState(); // Fork the post-state of the transaction
        address postOwner = ownable.owner(); // Get the owner after the transaction
        
        require(postOwner == preOwner, "Ownership has changed"); // Assert ownership hasn't changed
    }
}
```

### Key Components of an Assertion:

1. **Inheritance**: All assertions must inherit from the `Assertion` base contract.
2. **Constructor**: Initialize the contract(s) you want to check.
3. **Triggers Function**: Register which assertion functions should be triggered.
4. **Assertion Functions**: Implement the actual checks using pre and post transaction states.

## 3. Testing Your Assertion

To test your assertion, create a test file in the `assertions/test` directory:

Create a file `assertions/test/OwnableAssertion.t.sol`:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.28;

import {Test} from "forge-std/Test.sol";
import {Credible} from "credible-std/Credible.sol";
import {OwnableAssertion} from "../src/OwnableAssertion.sol";
import {Ownable} from "../../src/Ownable.sol";

contract TestOwnableAssertion is Test, Credible {
    Ownable public ownable;

    function setUp() public {
        ownable = new Ownable();
    }

    function test_assertionOwnershipChange_NoChange() public {
        OwnableAssertion assertion = new OwnableAssertion(address(ownable));
        
        // This should pass as we're not changing ownership
        assertion.assertionOwnershipChange();
    }
    
    function test_assertionOwnershipChange_WithChange() public {
        OwnableAssertion assertion = new OwnableAssertion(address(ownable));
        
        // Transfer ownership to a new address
        address newOwner = address(0x123);
        ownable.transferOwnership(newOwner);
        
        // This should fail as ownership has changed
        vm.expectRevert("Ownership has changed");
        assertion.assertionOwnershipChange();
    }
}
```

## 4. Running Tests

Use the PCL CLI to run your tests:

```bash
pcl phorge test
```

This command will compile your assertion and run the tests. You should see output indicating that your tests have passed.

## 5. Building Your Assertion

Before you can submit your assertion, you need to build it:

```bash
pcl build
```

This will compile your assertion and generate the necessary artifacts.

## 6. Authenticating with PCL

Before submitting your assertion, you need to authenticate:

```bash
pcl auth login
```

This will open a browser window where you can connect your wallet and authenticate with the Credible Layer.

## 7. Storing Your Assertion

Next, store your assertion in the Assertion Data Availability layer:

```bash
pcl store OwnableAssertion
```

This command submits your assertion's bytecode and source code to be stored by the Assertion DA.

## 8. Submitting Your Assertion

Finally, submit your assertion to the Credible Layer dApp:

```bash
pcl submit
```

This will prompt you to select the project and assertion(s) you want to submit. Follow the interactive prompts to complete the submission.

Alternatively, you can specify the project and assertion directly:

```bash
pcl submit --project-name "MyProject" --assertion-name "OwnableAssertion"
```

## Conclusion

Congratulations! You've successfully created, tested, and submitted your first assertion using the PCL CLI. You can now create more complex assertions to verify various properties of smart contracts.

For more detailed information about the PCL CLI and its commands, see the [CLI Reference Guide](credible/pcl-reference).
